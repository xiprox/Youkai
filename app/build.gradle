apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
// 116 Travis.com builds which were lost with the move to Travis.org
def versionBuild = Integer.parseInt(System.getenv('TRAVIS_BUILD_NUMBER') ?: "1") + 116

def keystoreAlias = System.getenv('keystoreAlias') ?: project.property("signing.upload.alias")
def keystorePassword = System.getenv('keystorePassword') ?: project.property("signing.upload.password")

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = 'youkai-publish@api-7655273526229553849-892363.iam.gserviceaccount.com'
            pk12File = file('../key.p12')
        }
    }

    defaultConfig {
        applicationId "app.youkai"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        release {
            storeFile file("../keystore-upload.jks")
            storePassword keystorePassword
            keyAlias keystoreAlias
            keyPassword keystorePassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.resources.srcDirs += ['src/test/resources']
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    /* Any jar files */
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /* Android Constraint Layout
     * https://developer.android.com/training/constraint-layout/index.html */
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    /* Android Support Libraries
     * https://developer.android.com/topic/libraries/support-library/index.html */
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:cardview-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    compile "com.android.support:palette-v7:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"

    /* ErrorView
     * https://github.com/xiprox/ErrorView */
    compile 'tr.xip.errorview:library:3.0.0'

    /* Facebook's Fresco
     * Image loading
     * https://github.com/facebook/fresco */
    compile "com.facebook.fresco:fresco:$fresco_version"
    compile "com.facebook.fresco:animated-gif:$fresco_version"

    /* Google's Flexbox
     * https://github.com/google/flexbox-layout */
    compile 'com.google.android:flexbox:0.3.0'

    /* Jackson
     * JSON parsing
     * https://github.com/FasterXML/jackson-core */
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"

    /* JSONAPI converter
     * https://github.com/Kevinrob/jsonapi-converter */
    compile('com.github.dulleh:jsonapi-converter:090d7a89426c4765ea08edb655d5c0aa7e3c7d5d'){
        changing = true
    }

    /* Kotlin
     * https://kotlinlang.org/ */
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    /* MaterialRatingBar
     * https://github.com/DreaminginCodeZH/MaterialRatingBar */
    compile 'me.zhanghai.android.materialratingbar:library:1.0.2'

    /* Mosby
     * Model, View, Presenter
     * https://github.com/sockeqwe/mosby */
    compile 'com.hannesdorfmann.mosby:viewstate:2.0.1'

    /* PlaceholderTextView
     * https://github.com/youkai-app/PlaceholderTextView */
    compile 'app.youkai.placeholdertextview:library:1.1.1'

    /* Square's LeakCanary
     * Leak detection
     * https://github.com/square/leakcanary */
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    /* Square's OkHttp
     * Network Layer
     * https://github.com/square/okhttp */
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    /* Square's Retrofit
     * Type-safe HTTP Client
     * https://github.com/square/retrofit */
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-jackson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    /* Rx
     * https://github.com/ReactiveX/RxAndroid */
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0'

    /* Testing */
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/youkai/maven" }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
